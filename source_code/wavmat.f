      SUBROUTINE WAVMAT(W,N,R,P,VL,IV,ERED,EINT,CENT,EP2RU,CM2RU,
     1                  RSCALE,DIAG,MXLAM,NHAM,IPRINT)
C  Copyright (C) 2020 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE potential
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  EVALUATES THE POTENTIAL MATRIX W AT RADIUS R
C     W = VCOUPL + EINT + VCENT - ETOT
C  ORDER OF THE REAL SYMMETRIC MATRIX W IS N
C  THE FULL MATRIX IS COMPUTED
C  VL IS THE PREVIOUSLY COMPUTED MATRIX OF THE COUPLING POTENTIAL
C  IV IS AN INDEX ARRAY MAPPING P ONTO VL, SUCH THAT VL(I) IS
C           A COEFFICIENT TO MULTIPLY P(IV(I))
C  ERED IS THE TOTAL ENERGY ETOT IN REDUCED UNITS
C     ETOT*2.*MUNIT*URED*EP2CM*RUNIT**2/HBAR**2
C  EINT(I) IS THE REDUCED INTERNAL ENERGY OF THE I-TH CHANNEL
C       (BUT VL ARRAY IS USED INSTEAD OF EINT IF NCONST > 0)
C  CENT(I) IS L*(L+1) FOR THE I-TH CHANNEL
C       (BUT VL ARRAY IS USED INSTEAD OF CENT IF NRSQ = 1)
C  EP2RU IS THE CONVERSION FACTOR FROM EPSIL ENERGY UNITS TO REDUCED ENERGY
C  UNITS, EP2RU = MUNIT*URED*RUNIT**2*EPSIL/BFCT,
C  CONCEPTUALLY 2*MU/HBAR^2 IN APPROPRIATE UNITS
C
      DIMENSION W(N,N),VL(1),IV(1),EINT(N),CENT(N),P(NHAM),DIAG(N),
     1          IDUM1(1)
C
C     COMMON /SCALE / SCALAM
C
      RSQ=1.D0/(R*R)
C  TO PREVENT OVERFLOW...
      RSQ=MIN(RSQ,1.D16)
C
C  COMPUTE THE RADIAL PARTS OF THE POTENTIAL
C  IDUM1 AND IDUM2 ARE DUMMY ARGUMENTS HERE.
C
      CALL POTENL(0,MXLAM,IDUM1,R*RSCALE,P,IDUM2,IPRINT)
C
C  EXTEND ARRAY WITH ANY CONSTANT TERMS IN HAMILTONIAN,
C  CONVERTED TO POTENTIAL UNITS
C
      EP2CM=EP2RU/CM2RU
      DO I=1,NCONST
        P(MXLAM+I)=VCONST(I)/EP2CM
      ENDDO
C
C  PROCESS ANY POTENTIAL SCALING FACTOR IN SCALAM
C
      CALL SCAPOT(P,MXLAM)
C
C  ADD ANY PERTURBATIVE SHIFTS FOR EXPECTATION VALUES
C
      CALL PERTRB(R*RSCALE,P,NHAM,0)
C
C  CONVERT P ARRAY FROM POTENTIAL UNITS TO REDUCED UNITS
C
      DO 10 I=1,MXLAM+NCONST
        P(I)=EP2RU*P(I)
   10 CONTINUE
C
C  EXTEND ARRAY WITH ANY TERMS THAT MULTIPLY 1/R**2
C
      IF (NRSQ.GT.0) THEN
        DO I=1,NRSQ
          P(MXLAM+NCONST+I)=RSQ
        ENDDO
      ENDIF
C
      CALL WAVVEC(VL,P,IV,W,N,NHAM)
C
C  NOW COMPUTE THE DIAGONAL CONTRIBUTIONS W(I,I).
C
      IF (NRSQ.EQ.0) THEN
        DO 20 I=1,N
          W(I,I) = W(I,I) + RSQ*CENT(I)
   20   CONTINUE
      ENDIF

      IF (NCONST.EQ.0) THEN
        DO 30 I=1,N
          W(I,I) = W(I,I) + EINT(I)
   30   CONTINUE
      ENDIF

      DO 40 I=1,N
        W(I,I)  = W(I,I) - ERED
        DIAG(I) = W(I,I)
   40 CONTINUE
C
      RETURN
      END
