      SUBROUTINE EFPROP(N, RBEGIN, REND, NSTEP, DR, PSIB,
     1                  RAB, PSIA, IWREC, SUMPSI, IPRINT, IPREC,
     2                  LFIRST)
C  Copyright (C) 2020 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  ROUTINE TO PROPAGATE THE EIGENFUNCTION (WAVEFUNCTION) AT THE MATCHING
C  POINT GENERATING A VECTOR FOR IT AT EACH STEP IN THE PROPAGATION.
C  THE COUPLING MATRIX EVALUATED AT THE MIDPOINT OF EACH SECTOR
C  IS USED AS A REFERENCE POTENTIAL FOR THE SECTOR. THE MODIFIED
C  LOG DERIVATIVE PROPAGATOR OF MANOLOPOULOS IS USED. THIS VERSION
C  ADAPTED FROM DAPROP BY AE Thornley NOV 92
C
C  INTEGRATION BY SIMPSON'S RULE CORRECTED CR Le Sueur MAY 12
C  ROUTINE SIMPLIFIED TOO
C
C  FURTHER ADAPTIONS TO DEAL WITH THE POSSIBILITY OF MULTIPLE PROPAGATION
C  SEGMENTS BY CR Le Sueur NOV 2016
C
C  NOV 2016 CR Le Sueur
C  CHANGES PREPARATORY TO USING QUADRATURE WITH UNEQUAL SPACES:
C  R IS WRITTEN TO PROPAGATION FILE BY MDPROP, AND READ IN HERE, AND IS
C  ALSO WRITTEN OUT TO WAVEFUNCTION FILE.
C
C  JAN 2020 CR Le Sueur
C  NOW USES EXTENDED ALTERNATIVE SIMPSON'S RULE FOR CALCULATING
C  NORMALISATION FACTOR
C
C  ON ENTRY: RBEGIN}
C            REND  } ARE LIMITS OF RADIAL PROPAGATION VARIABLE,
C            N       IS THE SIZE OF THE BASIS,
C            NSTEP   IS THE NUMBER OF STEPS USED IN THE RADIAL PROPAGATION
C            DR      IS THE STEP LENGTH
C            PSIA    IS THE WAVEFUNCTION TO START PROPAGATING FROM
C            RAB     IS THE PROPAGATION MATRIX
C            IWREC   IS THE ADDRESS OF THE LAST W (RAB) MATRIX WRITTEN TO
C                    CHANNEL IWAVSC
C            IPREC   IS THE ADDRESS OF WHERE THE FIRST PSI IS TO BE WRITTEN
C                    TO ON CHANNEL IPSISC
C            LFIRST  IS A LOGICAL INDICATING THAT THIS IS THE FIRST
C                    SEGMENT AND SO TO WRITE OUT PSI AT THE FIRST STEP
C  DURING:   PSIA}
C            PSIB}   ARE WORKSPACE ARRAYS USED FOR THE WAVEFUNCTION
C
C  ON EXIT:  SUMPSI  CONTAINS THE ACCUMULATED NORMALISATION INTEGRALS
C
      DIMENSION PSIA(N),PSIB(N), SUMPSI(N), RAB(N,N)
      COMMON /IOCHAN/ IPSISC,IWAVSC,IPSI,NWCOL,PSIFMT
      LOGICAL PSIFMT

      LOGICAL LFIRST
C
C  THIS VERSION USES A CONSTANT STEP SIZE THROUGHOUT THE
C  INTEGRATION RANGE, WITH NSTEP STEPS BETWEEN RBEGIN AND REND.
C
      H=DR
      IF (REND.GT.RBEGIN) THEN
        IDIR=1
      ELSE
        IDIR=-1
      ENDIF
      R=REND
C
      DO 100 ISTEP=1,NSTEP+1
C  SET UP PARAMETERS FOR INTEGRATION BY ALTERNATIVE EXTENDED SIMPSON'S RULE
C  (SEE NUMERICAL RECIPES EQN 4.1.14)
        IF (ISTEP.EQ.1 .OR. ISTEP.EQ.NSTEP+1) THEN
          A=17D0/48D0
        ELSEIF (ISTEP.EQ.2 .OR. ISTEP.EQ.NSTEP) THEN
          A=59D0/48D0
        ELSEIF (ISTEP.EQ.3 .OR. ISTEP.EQ.NSTEP-1) THEN
          A=43D0/48D0
        ELSEIF (ISTEP.EQ.4 .OR. ISTEP.EQ.NSTEP-2) THEN
          A=49D0/48D0
        ELSE
          A=1D0
        ENDIF
C
C  WRITE OUT PSI TO WAVEFN SCRATCH FILE
C
        IF (ISTEP.NE.1 .OR. LFIRST) WRITE(IPSISC,REC=IPREC) R,PSIA

        IF (ISTEP.EQ.NSTEP+1) GOTO 105

        R=R-H
C
C  READ IN R(A,B) FROM WAVESCRATCH
C
        IWREC=IWREC-1
        READ(IWAVSC,REC=IWREC,ERR=900) RP,RAB
        IF (ABS(RP-R).GT.1D-8) THEN
          WRITE(6,*)' EFPROP: MISMATCH WITH R READ FROM FILE AT STEP =',
     1              ISTEP,', R = ',R,' AND RP (FROM FILE) = ',RP
          STOP
        ENDIF
        IPREC=IPREC-IDIR
C
C  GENERATE PSI(B) FROM PSI(A) AND R(A,B)
C
        CALL DGEMUL(RAB,N,'N',PSIA,N,'N',PSIB,N,N,N,1)

 105    DO I=1,N
C  COLLECT TERMS FOR THE NORMALISATION CONSTANT
          SUMPSI(I)=SUMPSI(I)+A*PSIA(I)*PSIA(I)*ABS(H)
C  COPY PSI(B) TO PSI(A) FOR NEXT SECTOR
          PSIA(I)=PSIB(I)
        ENDDO
        IF (IPRINT.GE.30) THEN
          WRITE(6,110) R,(PSIA(K),K=1,N)
 110      FORMAT(E12.5,7X,10(E12.5,5X))
        ENDIF
 100  CONTINUE
C
      RETURN
C
 900  WRITE(6,910) IWREC
 910  FORMAT('  *** ERROR - CRASHED ON READ OF RAB FILE - REC=',I6)
      END
