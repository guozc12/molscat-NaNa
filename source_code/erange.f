      SUBROUTINE ERANGE(INRG,ICHAN,JFIELD,LENEFV,NBASIS,
     1                  WVEC,SCLEN,AWVMAX,RUNAME,IPRINT)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      IMPLICIT NONE
C  ROUTINE TO CALCULATE EFFECTIVE RANGE FROM EXPANSIONS IN SCATTERING
C  LENGTH AND IN RECIPROCAL OF SCATTERING LENGTH

      SAVE XK2REF,SCLREF

      INTEGER,          INTENT(IN) :: INRG,ICHAN,JFIELD,LENEFV,
     1                                NBASIS(*),IPRINT
      DOUBLE PRECISION, INTENT(IN) :: WVEC(*),SCLEN,AWVMAX
      CHARACTER(10),    INTENT(IN) :: RUNAME

      DOUBLE PRECISION :: XK2REF,SCLREF,XK2NOW,SCLNOW,RNGEFF,SCLZER

      IF (ICHAN.LE.0) RETURN
      IF (INRG.EQ.1) THEN
        XK2REF=WVEC(NBASIS(ICHAN))**2
        SCLREF=DBLE(SCLEN)
        RETURN
      ELSE
        XK2NOW=WVEC(NBASIS(ICHAN))**2
        SCLNOW=DBLE(SCLEN)
        IF (IPRINT.GE.1  .AND. XK2NOW.LT.AWVMAX**2 .AND.
     1                         XK2REF.LT.AWVMAX**2) THEN
          RNGEFF=2.D0*(1.D0/SCLREF-1.D0/SCLNOW)/
     1           (XK2NOW-XK2REF)
          SCLZER=1.D0/SCLREF+0.5D0*XK2REF*RNGEFF
          IF (LENEFV.GT.0) THEN
            WRITE(6,2700) JFIELD,ICHAN
          ELSE
            WRITE(6,2701) ICHAN
          ENDIF
          WRITE(6,2710) '1/A',INRG,RNGEFF,TRIM(RUNAME),
     1                  '1/A',SCLZER,TRIM(RUNAME)//'^-1'
          RNGEFF=2.D0*(SCLNOW-SCLREF)/(XK2NOW-XK2REF)
          SCLZER=SCLREF-0.5D0*XK2REF*RNGEFF
          RNGEFF=RNGEFF/SCLZER**2
          WRITE(6,2710) ' A ',INRG,RNGEFF,TRIM(RUNAME),
     1                  '  A',SCLZER,TRIM(RUNAME)
 2700     FORMAT(/'  EFFECTIVE RANGE AT EFV SET ',I3,
     1            ' FOR CHANNEL',I4,':')
 2701     FORMAT(/'  EFFECTIVE RANGE  FOR CHANNEL',I4,':')
 2710     FORMAT('  FROM EXPANSION OF ',A3,
     2           ' AT ENERGIES 1 AND',I4,' IS',G13.5,1X,A,
     3           ' WITH ',A3,'(K=0) =',G19.10,1X,A)
        ENDIF
      ENDIF

      RETURN
      END
