      FUNCTION PLM(LIN,MINM,COSTH)
C  This function is part of the MOLSCAT, BOUND and FIELD suite of programs
C
C  THIS ROUTINE APPEARS TO BE MUCH MORE STABLE FOR LARGE L, M THAN
C  THE ROUTINE FROM NERF/ MODIFIED ACCORDING TO R.T PACK
C
C  IT WAS OBTAINED:
C  FROM: MARIE-LISE DUBERNET <MLD@IPP-GARCHING.MPG.DE>
C  DATE: MON, 19 JUN 1995 12:48:11 +0200 (MET DST)
C  SOME MODS 27-28 JUNE 95 BY S Green FOR SPEED AND TO ACCORD W/ MOLSCAT
C  BUGS FIXED 21 SEPT 95 (S Green)
C
C  COMPUTES NORMALIZED ASSOCIATED LEGENDRE POLYNOMIALS BY RECURSION.
C  THE VALUES RETURNED ARE NORMALIZED FOR INTEGRATION OVER X
C  (I.E. INTEGRATION OVER COS THETA BUT NOT PHI).
C  NOTE THAT THE NORMALIZATION GIVES
C        PLM(L,0,1)=SQRT(L+0.5)
C        PLM(L,0,X)=SQRT(L+0.5) P(L,X)
C  FOR M.NE.0, THE VALUE RETURNED DIFFERS FROM THE USUAL
C        DEFINITION OF THE ASSOCIATED LEGENDRE POLYNOMIAL
C        (E.G. EDMONDS PAGES 23-24)
C        BY A FACTOR OF (-1)**M*SQRT(L+0.5)*SQRT((L-M)!/(L+M)!)
C  THUS THE SPHERICAL HARMONICS ARE
C       CLM = PLM * EXP(I*M*PHI) / SQRT(L+0.5)
C       YLM = PLM * EXP(I*M*PHI) / SQRT(2*PI)
C  THIS ROUTINE ALWAYS RETURNS THE VALUE FOR ABS(MINM); NOTE THAT
C       FOR MINM.LT.0 THIS VALUE SHOULD BE MULTIPLIED BY PARSGN(MINM)
C
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
C
C  CHECK FOR ABS(COSTH).LE.1.D0 ...
      IF (ABS(COSTH).GT.1.D0) THEN
        WRITE(6,*) ' *** ILLEGAL ARGUMENT TO PLM. X =',COSTH
        STOP
      ENDIF
C  SAVE ARGUMENTS IN LOCAL VARIABLES
      L=LIN
      M=ABS(MINM)
      X=COSTH
C
C  IF M>L PLM=0 !
      IF (M.GT.L) THEN
        PLM=0.D0
        RETURN
      ENDIF
      LMAX=L
C
      IF (M.GT.0) GOTO 5
C  HERE FOR REGULAR LEGENDRE POLYNOMIALS
      PLM=1.D0
      PM2=0.D0
      XL=0.D0
      DO 2 L=1,LMAX
        XL=XL+1.D0
        PP=((2.D0*XL-1.D0)*X*PLM-(XL-1.D0)*PM2)/XL
        PM2=PLM
    2   PLM=PP
      GOTO 9000
C
C  HERE FOR ALEXANDER-LEGENDRE POLYNOMIALS
C
    5 IMAX=2*M
      RAT=1.D0
      AI=0.D0
      DO 6 I=2,IMAX,2
        AI=AI+2.D0
    6   RAT=RAT*((AI-1.D0)/AI)
      Y=SQRT(1.D0-X*X)
      PLM=SQRT(RAT)*(Y**M)
      PM2=0.D0
      LOW=M+1
      XL=LOW-1
      DO 10 L=LOW,LMAX
        XL=XL+1.D0
        AL=DBLE((L+M)*(L-M))
        AL=1.D0/AL
        AL2=(DBLE((L+M-1)*(L-M-1)))*AL
        AL=SQRT(AL)
        AL2=SQRT(AL2)
        PP=(2.D0*XL-1.D0)*X*PLM*AL-PM2*AL2
        PM2=PLM
   10   PLM=PP
      PLM=PLM*PARSGN(MINM)
C
C  CONVERT TO MOLSCAT'S IDIOSYNCRATIC NORMALIZATION
9000  PLM=PLM*SQRT(XL+0.5D0)
      RETURN
      END
